<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		title="Browse Sets"
		destructionPolicy="never"
		viewActivate="viewActivateHandler(event)">

	<s:states>
			<s:State name="loadingState"/>
			<s:State name="errorState"/>
			<s:State name="completeState"/>
	</s:states>
	
	<fx:Script>
		<![CDATA[
			import VOs.DataObject;
			import VOs.SetVO;
			
			import com.adobe.webapis.flickr.FlickrService;
			import com.adobe.webapis.flickr.events.FlickrResultEvent;
			
			import mx.collections.ArrayCollection;
			
			import spark.events.ViewNavigatorEvent;
			
			import statics.NSID;
			import statics.ViewTypes;
			
			[Bindable]
			private var _service:FlickrService;
			[Bindable]
			private var _setsData:ArrayCollection;
			[Bindable]
			private var _setVOs:ArrayCollection;
			
			Security.loadPolicyFile("http://farm1.static.flickr.com/crossdomain.xml");  
			Security.loadPolicyFile("http://farm2.static.flickr.com/crossdomain.xml");  
			Security.loadPolicyFile("http://farm3.static.flickr.com/crossdomain.xml");  
			Security.loadPolicyFile("http://farm4.static.flickr.com/crossdomain.xml");
			
			private function viewActivateHandler(event:ViewNavigatorEvent):void{
				_service = data as FlickrService;
				getSetList();
			}
			
			private function onClickTryAgain():void{
				getSetList();
			}
			
			private function getSetList(event:MouseEvent=null):void{
					currentState = "loadingState";
					if(!_service)_service = data as FlickrService;
					trace("data: "+data+" _service: "+_service+" nsid: "+NSID.getNSID());
					_service.photosets.getList(NSID.getNSID());
					_service.addEventListener(FlickrResultEvent.PHOTOSETS_GET_LIST, handleSetList); 
			} 
				
			private function handleSetList(event:FlickrResultEvent):void{ 
				trace("handleSetList: e.data.photoSets: "+event.data.photoSets);
				if(event.success){
					var setData:Array = event.data.photoSets as Array; 
					_setVOs = new ArrayCollection(new Array);
		
					for(var i:uint = 0; i < setData.length; i ++){ 
						var vo:SetVO = new SetVO();
						vo.id = setData[i].id;
						vo.title = setData[i].title;
						vo.service = _service;
						_setVOs.addItem(vo);
					}
					
					currentState = "completeState";
					_service.removeEventListener(FlickrResultEvent.PHOTOSETS_GET_LIST, handleSetList);
				
				}else{
					
					currentState = "errorState";
				
				}
			} 
			
			private function onClickList():void{
				trace("id" +setList.selectedItem.id);
				var dataObject:DataObject = new DataObject();
				dataObject.selectedSet = setList.selectedItem;
				dataObject.viewType = ViewTypes.SET;
				navigator.pushView(MultiplePhotosView,dataObject);
			}
			
		]]>
	</fx:Script>
	
	<!--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~State~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-->
	
	<s:VGroup horizontalCenter="0" verticalCenter="0" includeIn="loadingState">
		<s:BusyIndicator scaleX="3" scaleY="3"/>
		<s:Label text="Loading..."/>
	</s:VGroup>
	
	<!--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~errorState~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-->
	
	<s:VGroup horizontalCenter="0" verticalCenter="0" includeIn="errorState">
		<s:Label text="Error loading list"/>
		<s:Button label="Try again" click="onClickTryAgain()"/>
	</s:VGroup>
	
	<!--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~completeState~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-->
	
	<s:VGroup width="100%" height="100%" 
			  paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5"
			  includeIn="completeState">
		
	<s:List id="setList" width="100%" height="100%"
			change="onClickList()" 
			dataProvider="{_setVOs}">
		
		<s:itemRenderer>
			<fx:Component>
				<s:IconItemRenderer labelField="title"/>
			</fx:Component>
		</s:itemRenderer>

	</s:List>
	</s:VGroup>
	
</s:View>